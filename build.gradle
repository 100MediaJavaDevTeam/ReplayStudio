buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

defaultTasks 'clean', 'build', 'shadowJar', 'install'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'
apply plugin: 'maven'

// Methods to determine detailed version string
def gitDescribe() {
    try {
        // determine git-describe output
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty=*'
            standardOutput = out
        }
        return out.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

group = 'com.replaymod'
description = 'ReplayStudio'
version = '0.0.1-SNAPSHOT'
mainClassName = 'com.replaymod.replaystudio.launcher.Launcher'

ext.projectName = 'ReplayStudio'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'
ext.inceptionYear = '2016'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://repo.spacehq.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'org.spacehq:mcprotocollib:1.8.8-SNAPSHOT'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.google.guava:guava:18.0'
    compile "org.projectlombok:lombok:1.16.6"
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.guava:guava-testlib:18.0'
    testCompile 'pl.pragmatists:JUnitParams:1.0.4'
}

license {
    ext.name = project.projectName
    ext.author = project.author
    ext.url = project.authorUrl
    ext.year = project.inceptionYear
    header new File(project.getProjectDir(), 'HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    exclude 'LICENSE.txt'
    exclude 'META-INF/*'
}

shadowJar {
    append('META-INF/NOTICE.txt')
}

eclipse.project {
        name = 'ReplayStudio'
}

jar.manifest.mainAttributes(
        'Main-Class': mainClassName,
        'Implementation-Title': description,
        'Implementation-Version': gitDescribe()
)
