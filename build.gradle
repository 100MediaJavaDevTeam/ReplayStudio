buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:2.5.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.6'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

defaultTasks 'clean', 'build', 'shadowJar', 'install'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'
apply plugin: 'errorprone'

// Methods to determine detailed version string
def gitDescribe() {
    try {
        // determine git-describe output
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty=*'
            standardOutput = out
        }
        return out.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

group = 'de.johni0702'
description = 'ReplayStudio'
version = '0.0.1-SNAPSHOT'
mainClassName = 'de.johni0702.replaystudio.launcher.Launcher'
ext.lombok = 'org.projectlombok:lombok:1.14.8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://repo.johni0702.de/content/groups/public/'
    }
}

dependencies {
    compile 'org.spacehq:mcprotocollib:1.8-SNAPSHOT'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-cli:commons-cli:1.2'
    compile lombok
    compile 'com.google.guava:guava:12.0'
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.guava:guava-testlib:18.0'
    testCompile 'pl.pragmatists:JUnitParams:1.0.4'
}

shadowJar {
    append('META-INF/NOTICE.txt')
    dependencies {
        exclude(dependency(lombok))
    }
    //include 'lombok/Lombok.class'
}

retrolambda {
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

eclipse.project {
        name = 'ReplayStudio'
}

jar.manifest.mainAttributes(
        'Main-Class': mainClassName,
        'Implementation-Title': description,
        'Implementation-Version': gitDescribe()
)
