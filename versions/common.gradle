apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'
apply plugin: 'maven'
apply plugin: 'ch.raffael.preshadow'
apply plugin: 'de.johni0702.reproducible-builds'

def gitCommitHash() {
    try {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = out
        }
        return out.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

group = 'com.replaymod.replaystudio'
description = 'ReplayStudio'
version = gitCommitHash()
mainClassName = 'com.replaymod.replaystudio.launcher.Launcher'

ext.projectName = 'ReplayStudio'
ext.packaging = 'jar'
ext.author = 'johni0702'
ext.authorUrl = 'https://github.com/johni0702'
ext.inceptionYear = '2016'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def mcVersion
if (project.name != 'core') {
    def (major, minor, patch) = project.name.tokenize('.')
    mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
} else {
    def f = file('mcVersion')
    mcVersion = f.exists() ? f.readLines().first() as int : 11404
}

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.viaversion.com'
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    preshadow group: 'com.github.ReplayMod', name: 'MCProtocolLib', version: [
        11404: '1ec9844',
        11302: '777bd45',
        11202: '556c9f2',
        11201: '0220bc1',
        11200: 'cb65733',
        11102: '35a0eb6',
        11100: '27ef52c',
        11002: 'be3fafb',
        10904: 'bc4b0b8',
        10800: '382d2d6',
        10710: '63c1212',
    ][mcVersion]

    preshadow 'com.github.replaymod.viaversion:viaversion-common:506171d7'

    // ViaVersion will use the relocated netty of MCProtocolLib
    // un-shaded from ViaVersion
    compile 'com.google.guava:guava:17.0'
    compile 'com.google.code.gson:gson:2.3.1'

    // Un-shaded from MCProtocolLib, also updated to 1.2 for ViaVersion
    compile 'com.github.steveice10:opennbt:d50274d' // 1.2

    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-cli:commons-cli:1.2'
    compileOnly "org.projectlombok:lombok:1.16.6"
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.guava:guava-testlib:18.0'
    testCompile 'pl.pragmatists:JUnitParams:1.0.4'
}

license {
    ext.name = project.projectName
    ext.author = project.author
    ext.url = project.authorUrl
    ext.year = project.inceptionYear
    header project.parent.file('HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    exclude 'LICENSE.txt'
    exclude 'META-INF/*'
    exclude 'com/github/steveice10/packetlib/packet/PacketProtocol.java'
}

preshadowJar {
    if (mcVersion < 10904) {
        relocate 'org.spacehq.mc.protocol.data.game.values', 'com.github.steveice10.mc.protocol.data.game'
    }
    if (mcVersion < 11200) {
        relocate 'org.spacehq', 'com.github.steveice10'
    }
    relocate 'io.netty', 'com.github.steveice10.netty'
    relocate 'us.myles.ViaVersion', 'com.replaymod.replaystudio.us.myles.ViaVersion'

    // un-shading from ViaVersion (see dependencies)
    exclude 'com/google/gson/**' // gson
    exclude 'com/google/common/**' // guava
    exclude 'com/google/thirdparty/publicsuffix/**' // also part of guava
    // don't need this one, ViaVersion uses it to parse configs (which we never need to do)
    exclude 'org/yaml/snakeyaml/**' // snakeyaml

    // un-shading from MCProtocolLib (see dependencies)
    exclude 'com/github/steveice10/opennbt/**' // opennbt
    exclude 'org/spacehq/opennbt/**' // opennbt (pre 1.12)
}

shadowJar {
    archiveName = "${baseName}-${version}.full.${extension}"
    append('META-INF/NOTICE.txt')
    from(tasks.findByName('preshadowJar').outputs.files.collect {it.isDirectory() ? it : zipTree(it)})
}
build.dependsOn shadowJar

jar.manifest.mainAttributes(
        'Main-Class': mainClassName,
        'Implementation-Title': description,
        'Implementation-Version': gitCommitHash()
)

if (project.name != 'core') {
    apply from: '../preprocessor.gradle'

    def preprocessedSrc = 'build/preprocessed/src'
    def preprocessedRes = 'build/preprocessed/res'
    def preprocessedTestSrc = 'build/preprocessed/test'
    def originalSrc = '../../src/main/java'
    def originalRes = '../../src/main/resources'
    def originalTestSrc = '../../src/test/java'
    def vars = [MC: mcVersion]

    sourceSets {
        main.java.srcDir preprocessedSrc
        main.resources.srcDir preprocessedRes
        test.java.srcDir preprocessedTestSrc
    }

    task preprocessJava {
        inputs.dir(originalSrc)
        outputs.dir(preprocessedSrc)
        doLast {
            project.convertTree(vars, originalSrc, preprocessedSrc)
        }
    }

    task preprocessResources {
        inputs.dir(originalRes)
        outputs.dir(preprocessedRes)
        doLast {
            project.convertTree(vars, originalRes, preprocessedRes)
        }
    }

    task preprocessTestJava {
        inputs.dir(originalTestSrc)
        outputs.dir(preprocessedTestSrc)
        doLast {
            project.convertTree(vars, originalTestSrc, preprocessedTestSrc)
        }
    }

    compileJava.dependsOn preprocessJava
    processResources.dependsOn preprocessResources
    compileTestJava.dependsOn preprocessTestJava
} else {
    sourceSets {
        main.java.srcDirs = ['../../src/main/java']
        main.resources.srcDirs = ['../../src/main/resources']
        test.java.srcDirs = ['../../src/test/java']
    }
}
